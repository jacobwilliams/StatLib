	SUBROUTINE RNORTM(N, NP1, IBCONF, NB, B, NDBI, DBI, ISEED, A,
     *  CHISQ, FAB, U, W, IFAULT)
C
C	  ALGORITHM AS 127 APPL. STATIST. (1978) VOL.27, NO.2
C
C	  RNORTM GENERATES ORTHOGONAL MATRICES  A  FROM A DISTRIBUTION
C	  WITH DENSITY FUNCTION DEFINED ON THE GROUP OF ORTHOGONAL
C	  MATRICES.  WHEN THE INPUT PARAMETER IBCONF = 1 THE
C	  MATRICES ARE GENERATED FROM THE INVARIANT HAAR MEASURE.
C
C	Corrections in remark AS R42 (Appl. Statist., vol.31, 1982) have
C	been incorporated.
C
	REAL B(NB), DBI(NDBI), A(N, N), CHISQ(N), U(NP1), W(N), FAB, UL,
     *  WL, WIL, WWIL, T2, HV, HW, ZERO, ONE
C
	DATA ZERO /0.0/, ONE /1.0/
C
C	  STATEMENT FUNCTIONS FOR DOUBLE PRECISION
C	  DOUBLE PRECISION SQRT, SIGN, ABS
C	  SQRT(UL) = DSQRT(UL)
C	  SIGN(UL, WL) = DSIGN(UL, WL)
C	  ABS(UL) = DABS(UL)
C
C	  SECTION 1   INITIALIZATION
C	  CHECK CONSISTENCY OF INPUT DIMENSIONS
C
	IFAULT = 0
	IF (N .LT. 1) GOTO 1
	IF (NP1 .NE. N + 1) GOTO 1
	IF (IBCONF .LT. 1 .OR. IBCONF .GT. 3) GOTO 1
	IF (IBCONF .EQ. 1 .AND. (NB .NE. 1 .OR. NDBI .NE. 1)) GOTO 1
	IF (IBCONF .EQ. 2 .AND. (NB .NE. N * (N + 1) / 2 .OR. NDBI .NE. N))
     *  GOTO 1
	IF (IBCONF .EQ. 3 .AND. (NB .NE. N * (N + 1) * (2 * N + 1) / 6
     *  .OR. NDBI .NE. N)) GOTO 1
	GOTO 4
C
C	  ERROR RETURNS
C
1	IFAULT = 1
	RETURN
2	IFAULT = 2
	RETURN
C
C	  INITIALIZE DENSITY, H(STORED IN A), AND IBSUB
C
4	FAB = ONE
	DO 100 I = 1, N
  	  DO 50	J = 1, N
50	  A(I, J) = ZERO
	  A(I, I) = ONE
100	CONTINUE
	IBSUB = 0
C
C	  CONSTRUCT A ONE COLUMN AT A TIME
C
	DO 315 L = 1, N
	  NP1L = NP1 - L
C
C	  SECTION 2
C	  CONSTRUCT U(L...N), UNIFORMLY DISTRIBUTED ON THE SURFACE OF THE
C	  (N+1-L)-SPHERE OF RADIUS UL = SQRT(CHISQ(L)).
C
	  CALL NORMAL(U(L), NP1L + 1, ISEED, CHISQ(L))
	  UL = SQRT(CHISQ(L))
	  IF (UL .EQ. ZERO) GOTO 2
	  IF (L .EQ. N) GOTO 301
C
C	  SECTION 3
C	  CALCULATE  W = B*U  AND STORE ITS LENGTH IN WL.
C	  SIGN OF WL PREVENTS LOSS OF SIGNIFICANCE IN WWIL
C
	  GOTO (215, 220, 225), IBCONF
C
C	  B IS IDENTITY
C
215	  WL = SIGN(UL, U(L))
	  DO 219 J = L, N
219	  W(J) = U(J)
	  GOTO 229
C
C	  DIAGONAL MATRICES B
C
220	  WL = ZERO
	  DO 221 J = L, N
	    ITEMP = IBSUB + J
	    T2 = B(ITEMP) * U(J)
	    WL = WL + T2 * T2
	    W(J) = T2
221	  CONTINUE
	  IBSUB = IBSUB + N - L
	  GOTO 228
C
C	  FULL MATRICES B
C
225	  WL = ZERO
	  DO 227 J = L, N
	    T2 = ZERO
	    DO 226 K = L, N
	      ITEMP = IBSUB + K
	      T2 = T2 + B(ITEMP) * U(K)
226	    CONTINUE
	    IBSUB = IBSUB + NP1L
	    WL = WL + T2 * T2
	    W(J) = T2
227	  CONTINUE
	  IBSUB = IBSUB - 1
C
C	  CALCULATE DENSITY FOR IBCONF = 2 OR 3
C
228	  WL = SIGN(SQRT(WL), -W(L))
	  FAB = (WL / UL) ** NP1L * DBI(L) * FAB
	  IF (FAB .EQ. ZERO) GOTO 2
229	  CONTINUE
C
C	  SECTION 4
C	  PROJECT W ONTO ORTHOGONAL COMPLEMENT OF FIRST L-1
C	  COLUMNS OF A, NORMALIZE, AND STORE IN L COLUMN OF A.
C	  A(L) = H(L) * W * WIL
C	  CALCULATE PROJECTION MATRIX H(L) FOR ORTHOGONAL
C	  COMPLEMENT OF FIRST L COLUMNS OF A AND STORE IN LAST
C	  N-L COLUMNS OF A.
C
	  LP1 = L + 1
	  WIL = ONE / WL
	  WWIL = ONE / (WL - W(L))
	  DO 250 I = 1, N
	    T2 = ZERO
	    DO 230 J = L, N
230	    T2 = T2 + A(I, J) * W(J)
	    HV = T2 * WIL
	    HW = (HV - A(I, L)) * WWIL
	    A(I, L) = HV
	    DO 240 J = LP1, N
240	    A(I, J) = A(I, J) - HW * W(J)
250	  CONTINUE
C
C	  END OF L COLUMN OF MATRIX A
C
C	  SECTION 5
C	  WHEN L = N ONLY SIGN NEEDS TO BE CHOSEN
C	  REMEMBER MATRIX H(N) IS STORED IN N COLUMN OF A.
C
301	IF (U(L) .GT. ZERO) GOTO 315
	DO 310 I = 1, N
310	A(I, L) = -A(I, L)
315  CONTINUE
C
C	  FAB = ABS(B(LAST) * DBI(N) * FAB)
C
320	FAB = ABS(FAB)
C
	RETURN
	END
