      SUBROUTINE SCOUNT(NEVENT,TZERO,NF,NW,T,SPEC,FREQ,C,S,IFAULT)
C
C<<<<<  Acquired in machine-readable form from 'Applied Statistics'
C<<<<<  algorithms editor, January 1983.
C
C     ALGORITHM AS 150 APPL. STATIST. (1980) VOL.29, NO.2
C
C     COMPUTES ESTIMATES OF THE SPECTRUM OF A POINT PROCESS
C     BY TAKING A CENTRED MOVING AVERAGE OF THE NORMALISED
C     PERIODOGRAM OF THE COUNTING PROCESS
C
C
      DIMENSION T(NEVENT),FREQ(NF),SPEC(NF),C(NF),S(NF)
      REAL TWOPI
      DATA TWOPI,NRECUR/6.283185307196E0,100/
C
C      TEST FOR ERRORS
C
      IFAULT = 0
      IF(NW.LE.0) GO TO 10
      IF(NW.GT.NF) GO TO 11
      IF(MOD(NW,2).EQ.0) GO TO 12
      IF(NEVENT.LE.0) GO TO 13
      IF(TZERO.LE.0.0) GO TO 14
      IF(T(1).LT.0.0 .OR. T(NEVENT).GT.TZERO) GO TO 15
      DO 1 I = 2,NEVENT
      IF(T(I).LE.T(I-1)) GO TO 15
    1 CONTINUE
C
C     END OF ERROR-TESTING SECTION
C
      FW = NW
      FN = NEVENT
      RTZERO = 1.0/TZERO
C
C     INITIALISE ARRAYS FOR SUMS OF SINES AND COSINES
C     AND CALCULATE FREQUENCIES
C
      DO 2 I = 1,NF
      C(I) = 0.0
      S(I) = 0.0
      SPEC(I) = I
      FREQ(I) = SPEC(I)*RTZERO
    2 CONTINUE
C
C     CALCULATE SUMS OF SINES AND COSINES USING THE DOUBLE-
C     ANGLE SINE AND COSINE FORMULAS - EXCEPT COMPUTE EVERY
C     (NRECUR)TH. DIRECTLY
C
      DO 6 I = 1,NEVENT
      ANGLE = TWOPI*T(I)*RTZERO
      COSINI = COS(ANGLE)
      SININI = SIN(ANGLE)
      COSREC = 1.0
      SINREC = 0.0
      DO 5 J = 1,NF
C
C     TEST WHETHER NEED TO COMPUTE SINE AND COSINE DIRECTLY
C
      IF(MOD(J,NRECUR).NE.0) GO TO 3
C
C     RECALL THAT SPEC(J) = J
C
      FMULT = SPEC(J)*ANGLE
      COSREC = COS(FMULT)
      SINREC = SIN(FMULT)
      GOTO 4
    3 TEMP = COSREC*COSINI - SINREC*SININI
      SINREC = COSREC*SININI + SINREC*COSINI
      COSREC = TEMP
    4 C(J) = C(J) + COSREC
      S(J) = S(J) + SINREC
    5 CONTINUE
    6 CONTINUE
C
C     S(.) AND C(.) NOW CONTAIN THE SUMS OF SINES AND
C            COSINES,RESPECTIVELY
C
C
C     NOW USE SPEC(.) FOR THE NORMALISED PERIODOGRAM
C
      CNORM = 2.0/FN
      DO 7 I = 1,NF
    7 SPEC(I) = CNORM*(C(I)*C(I) + S(I)*S(I))
C
C     NOW OBTAIN THE SMOOTHED SPECTRAL ESTIMATES -
C     RETURN IF WANT ONLY THE PERIODOGRAM
C
      IF(NW.EQ.1) RETURN
      KU = NF - NW + 1
      L = NW/2
      DO 9 I = 1,KU
      SUM = 0.0
      J = I + NW - 1
      DO 8 K = I,J
    8 SUM = SUM + SPEC(K)
C
C     STORE THE SMOOTHED ESTIMATES AND THE CORRESPONDING
C     FREQUENCIES IN THE FIRST (NF - NW + 1) ELEMENTS OF
C         SPEC(.) AND FREQ(.)
C
      SPEC(I) = SUM/FW
      M = I + L
      FREQ(I) = FREQ(M)
    9 CONTINUE
      RETURN
   10 IFAULT = 1
      RETURN
   11 IFAULT = 2
      RETURN
   12 IFAULT = 3
      RETURN
   13 IFAULT = 4
      RETURN
   14 IFAULT = 5
      RETURN
   15 IFAULT = 6
      RETURN
      END
