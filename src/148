      SUBROUTINE JKKN(X, XX, K, N, TH, THJ, LTH, COV, PS, NGRP, IGSIZE,
     *  IFAULT)
C
C        REMARK ASR62 ON AS 148 APPL. STATIST. (1980) VOL. 29 NO. 1.
C
C        REMOVAL OF BIAS BY THE JACKKNIFE PROCEDURE
C
      INTEGER K, N, LTH, NGRP, IGSIZE, IFAULT
      REAL X(K, N), TH(LTH), THJ(LTH), COV(LTH, LTH), PS(LTH, NGRP),
     *  XX(K, N)
C
      IFAULT = 1
      IF (NGRP .LE. 1) RETURN
      IFAULT = 0
      NGRP1 = NGRP - 1
      NN = NGRP1 * IGSIZE
      ENGRP = NGRP
      ENGRP1 = NGRP1
      ENN = ENGRP * ENGRP1
      IGSZ1 = IGSIZE + 1
      KI = K * IGSIZE
C
C        KI = NUMBER OF X COMPONENTS IN A GROUP
C
      LIN = NGRP1 * KI
C
C        FIRST CALCULATE NGRP*THETAHAT AND STORE IT IN TH
C
      CALL THTHT(X, K, N, TH, LTH)
      DO 10 I = 1, LTH
   10 TH(I) = ENGRP * TH(I)
C
C        CALCULATE THE NGRP PSEUDOVALUES
C
C     IG1 = 0
C     DO 12 J = 1, K
C	DO 11 IN = I, NGRP
C  11   XX(J, IN) = X(J, IN)
C  12 CONTINUE
      DO 50 I = 1, NGRP
C
C        MOVE THE I-TH GROUP OF OBSERVATIONS TO THE NGRP-TH GROUP
C        POSITION, THEN MOVE POSITION OF THE I+1-TH GROUP I-TH
C        POSITION, I+2-TH TO I+1-TH AND SO ON
C
	DO 20 IG = 1, IGSIZE
	  DO 15 J = 1, K
	    TEMP = XX(J, I)
	    DO 14 IN = I, NN
	      NUM = IN + 1
   14       XX(J, IN) = XX(J, NUM)
	    XX(J, NGRP) = TEMP
   15     CONTINUE
   20   CONTINUE
	CALL THTHT(XX, K, NN, THJ, LTH)
	DO 22 J = 1, K
	  DO 21 IN = I, NGRP
   21     XX(J, IN) = X(J, IN)
   22   CONTINUE
	DO 35 II = 1, LTH
   35   PS(II, I) = TH(II) - THJ(II) * ENGRP1
   50 CONTINUE
C
C        INITIALIZE
C
      DO 75 I = 1, LTH
	THJ(I) = 0.0
	DO 75 J = 1, LTH
	  COV(I, J) = 0.0
   75 CONTINUE
C
C        CALCULATE JACKKNIFED VERSION OF THETAHAT AND STORE IN THJ
C
      DO 100 I = 1, NGRP
	DO 90 II = 1, LTH
   90   THJ(II) = THJ(II) + PS(II, I)
  100 CONTINUE
      DO 110 II = 1, LTH
  110 THJ(II) = THJ(II) / ENGRP
C
C        CALCULATE THE APPROXIMATE COVARIANCE MATRIC OF THE JACKKNIFED
C        THETAHAT AND STORE IT IN COV
C
      DO 125 II = 1, LTH
	THJII = THJ(II)
	DO 125 JJ = II, LTH
	  THJJJ = THJ(JJ)
	  DO 115 I = 1, NGRP
	    COV(II, JJ) = COV(II, JJ) + (PS(II, I) - THJII) * (PS(JJ, I)
     *                     - THJJJ)
  115     CONTINUE
  125 CONTINUE
C
C        IF LTH .GT. 1, COPY THE UPPER-TRIANGULAR SECTION OF COV INTO
C        THE LOWER TRIANGULAR SECTION
C
      IF (LTH .EQ. 1) GOTO 200
      DO 175 II = 2, LTH
	II1 = II - 1
	DO 175 JJ = 1, II1
	  COV(II, JJ) = COV(JJ, II)
  175 CONTINUE
  200 DO 210 II = 1, LTH
	DO 210 JJ = 1, LTH
  210 COV(II, JJ) = COV(II, JJ) / ENN
      RETURN
      END

